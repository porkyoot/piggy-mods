// build.gradle (Racine)
plugins {
	id 'fabric-loom' version "${loom_version}"
}



subprojects {
    apply plugin: 'java'
    apply plugin: 'fabric-loom'
    apply plugin: 'maven-publish'

    if (project.name == 'piggy-build' || project.name == 'piggy-inventory') {
        loom {
            splitEnvironmentSourceSets()
        }
    }

    apply plugin: 'maven-publish'

    //sourceCompatibility = JavaVersion.VERSION_21
    //targetCompatibility = JavaVersion.VERSION_21

    group = project.maven_group

    def shortName = project.name.replace("piggy-", "")
    
    // 2. On construit le nom de la variable qu'on cherche (ex: "build_version")
    def targetProperty = "${shortName}_version"
    
    // 3. On vérifie si cette variable existe dans gradle.properties
    if (rootProject.hasProperty(targetProperty)) {
        // Si oui, on l'utilise (ex: build_version = 1.2.0)
        version = rootProject.properties[targetProperty]
    } else {
        // Si non (ou pour un mod non listé), on utilise une version par défaut
        version = rootProject.properties["mod_version"]
    }

    repositories {
        mavenCentral()
        maven { url = 'https://maven.isxander.dev/releases' }
		maven {
            name = "Terraformers"
            url = "https://maven.terraformersmc.com/"
        }
    }

    dependencies {
        minecraft "com.mojang:minecraft:${project.minecraft_version}"
        mappings loom.officialMojangMappings()
        modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

        modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    }

    processResources {
        // Cette ligne force Gradle à refaire le fichier si la version change
        inputs.property "version", project.version

        filesMatching("fabric.mod.json") {
            // C'est ICI que la magie opère : on remplace ${version} par la vraie valeur
            expand "version": project.version
        }
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.release = 21
    }

    java {
        withSourcesJar()
    }

    publishing {
        publications {
            create("mavenJava", MavenPublication) {
                from components.java
            }
        }
        repositories {
            mavenLocal()
        }
    }
}

evaluationDependsOnChildren()

// Config for the root project to allow running everything together
repositories {
    mavenCentral()
    maven { url = 'https://maven.isxander.dev/releases' }
    maven {
        name = "Terraformers"
        url = "https://maven.terraformersmc.com/"
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    modImplementation "dev.isxander:yet-another-config-lib:${project.yacl_version}"
    modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"

    // Only add subproject dependencies if ALL subprojects exist (full repo).
    // This prevents issues when building individual modules in CI where the root
    // project would try to add a module as a dependency to itself.
    def hasAllSubprojects = findProject(":piggy-lib") != null &&
                            findProject(":piggy-admin") != null &&
                            findProject(":piggy-inventory") != null &&
                            findProject(":piggy-build") != null
    
    if (hasAllSubprojects && !project.hasProperty("bootstrap")) {
        // safeAdd checks if the project's jar exists before adding it as a dependency.
        // This prevents configuration failures during clean builds where Loom tries to read missing metadata.
        def safeAdd = { String projName ->
            def proj = project(projName)
            def libDir = new File(proj.buildDir, "libs")
            if (libDir.exists() && libDir.list()?.any { it.endsWith(".jar") }) {
                dependencies.add("modImplementation", proj)
            } else {
                println "Root Project: Skipping dependency ${projName} because jar not found. Run 'assemble' to build it."
            }
        }

        safeAdd(":piggy-lib")
        safeAdd(":piggy-admin")
        safeAdd(":piggy-inventory")
        safeAdd(":piggy-build")
    }
}

// Fix for runClient crashing due to gametest resources on classpath
afterEvaluate {
    tasks.named("runClient") {
        classpath = classpath.filter { 
            !it.path.contains("piggy-admin/build/resources/gametest") && 
            !it.path.contains("piggy-admin\\build\\resources\\gametest") 
        }
    }
}
